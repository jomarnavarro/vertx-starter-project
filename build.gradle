plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id "com.moowork.node" version "1.2.0"
}

ext {
    vertxVersion = '3.5.1'
    groovyVersion = "2.4.8"
}

repositories {
    mavenLocal()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
}

version = '1.0.0-SNAPSHOT'
sourceCompatibility = '1.8'

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-log4j12:1.7.25'

    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-rx-java:$vertxVersion"
    compile "io.vertx:vertx-lang-groovy:$vertxVersion"

    compile "io.vertx:vertx-web-client:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile "io.vertx:vertx-web-api-contract:$vertxVersion"
    compile "io.vertx:vertx-hazelcast:$vertxVersion"

    compile 'com.hazelcast:hazelcast-client:3.8.2'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile "io.vertx:vertx-unit:${vertxVersion}"
    testCompile 'junit:junit:4.12'
    testCompile 'cglib:cglib-nodep:3.2.5'
}

node {
    version = '6.4.0'
    npmVersion = '3.10.3'
    distBaseUrl = 'https://nodejs.org/dist'
    download = true
    workDir = file("${project.buildDir}/nodejs")
    nodeModulesDir = file("${project.projectDir}")
}

npmInstall {
    args = ['--prefix', 'src/main/resources/webroot']
}

mainClassName = "io.vertx.core.Launcher"
def mainVerticleName = "mx.jresendiz.SimpleChat"

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': 'mx.jresendiz.SimpleChat'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

def watchForChange = 'src/**/*.*'
def doOnChange = './gradlew classes'


run {
    systemProperty("vertx.logger-delegate-factory-class-name", "io.vertx.core.logging.SLF4JLogDelegateFactory")
    systemProperty("hazelcast.logging.type", "log4j")
    systemProperty("hazelcast.client.config", "src/main/resources/hazelcast-client.xml")
    systemProperty("hazelcast.config", "src/main/resources/cluster.xml")
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange", "-cluster"]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}